/*

     |     _    _|_   _|_    o
     |<   (_)    |_    |_    |


For help on multi-project build:
    https://docs.gradle.org/current/userguide/multi_project_builds.html

*/

def PUBLISH_VERSION = '0.2-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1.2-5'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // Upgrade junit-platform-gradle-plugin to M4 once https://github.com/JetBrains/spek/issues/195 is fixed and released.
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M3'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

//
// Projects Configuration
//

allprojects {

    apply plugin: 'java'

    group 'com.github.adeynack'
    version "$PUBLISH_VERSION"

}

subprojects {

    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'idea'
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    repositories {
        mavenCentral()
        jcenter()
        maven { url "http://dl.bintray.com/jetbrains/spek" }
    }

    dependencies {

        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

        testCompile 'org.mockito:mockito-core:2.7.22'
        testCompile 'org.amshove.kluent:kluent:1.19'
        testCompile "org.junit.platform:junit-platform-console:1.0.0-M3"
        testCompile('org.jetbrains.spek:spek-api:1.1.0') {
            exclude group: 'org.jetbrains.kotlin'
        }
        testCompile 'org.skyscreamer:jsonassert:1.4.0'

        testRuntime('org.jetbrains.spek:spek-junit-platform-engine:1.1.0') {
            exclude group: 'org.junit.platform'
            exclude group: 'org.jetbrains.kotlin'
        }

    }

    bintray {
        user = System.getenv()["BINTRAY_USER"]
        key = System.getenv()["BINTRAY_API_KEY"]
        publications = ['MavenPub']
        pkg {
            repo = 'kotti'
            name = 'kotti'
            licenses = ['MIT']
            vcsUrl = 'https://github.com/adeynack/kotti.git'
            version {
                name = "$PUBLISH_VERSION"
            }
        }
    }

    // Compile the tests with JVM 1.8 (ie: for testing against streams)
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    junitPlatform {
        platformVersion '1.0.0-M3'
        filters {
            engines {
                include 'spek'
            }
        }
    }

    publishing {
        publications {
            MavenPub(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                groupId 'com.github.adeynack'
                version "$PUBLISH_VERSION"
            }
        }
    }



}
